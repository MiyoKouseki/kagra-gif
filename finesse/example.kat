#------------------------------------------------------------------
# pdh-signal.kat test file for kat 0.70
# (Error signal of the Pound-Drever-Hall signal)
#
# freise@rzg.mpg.de 02.03.2002
#
# The "#" is used for comment lines.
#
# m1 m2
#        .-----.    .-.                           .-.
#        |     |    | |    . . . . . . . . . .    | |
# --> n0 | EOM | n1 | | n2 .       s1        . n3 | |
#        |     |    | |    . . . . . . . . . .    | |
#        `-----'    | |                           | |
#                   `-'                           `-'
#----------------------------------------------------------------
## reflectivity of first mirror set to 0.9 to get a `nice' plot
## the setting is different in `pdh.kat'!

m m1 0.9 0.0001 0 n1 n2     # mirror R=0.9, T=0.0001, phi=0
s s1 1200 n2 n3             # space L=1200m?
s s2 1 nn n1                # space L=1200m?
m m2 1 0 0 n3 dump          # mirror R=1 T=0 phi=0
l i1 1 0 n0                 # laser P=1W, f_offset=0Hz
mod eo1 40k 0.3 3 pm n0 nn  # phase modulator f_mod=40kHz
    	    	     	    # midx=0.3 order=3
pd1 inphase 40k 0 n1
pd1 quadrature 40k 90 n1
xaxis m2 phi lin -90 90 400
yaxis abs